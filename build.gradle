buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0-rc2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.5"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '1.0'
group = 'sk.seky.android'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        quiet true
        abortOnError false
        checkReleaseBuilds false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled false
            minifyEnabled false
            zipAlignEnabled false
        }
    }
}

// Still unsupported, see http://issues.gradle.org/browse/GRADLE-784
// Like this, it won't appear at all in the POM
configurations {
    provided
}

repositories {
    mavenCentral()
}

dependencies {
    provided 'com.google.android:android:4.1.1.4'
    compile 'com.android.support:appcompat-v7:23.+'

    compile 'com.github.tony19:logback-android-core:1.1.1-5'
    compile('com.github.tony19:logback-android-classic:1.1.1-5') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    // Jackson
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.2'
    compile('com.fasterxml.jackson.core:jackson-databind:2.7.2') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    }

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.jboss.resteasy:jaxrs-api:3.0.12.Final'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        aar(MavenPublication) {
            artifactId = 'android-webapp'
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'sekys'
        name = 'sk.seky.android.webapp'

        version {
            name = '1.0.0'
            desc = 'Android web-app'
            released = new Date()
            vcsTag = '1.0.0'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sekys.git'
        websiteUrl = 'https://github.com/sekys'
    }
    configurations = ['archives']
    //publications = ['aar']
}
